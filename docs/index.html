<!DOCTYPE html>
<html lang="en">
  <head>
    <meta charset="UTF-8">
    <link rel="icon" href="/favicon.ico">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>シャボン玉の物語 - IOR</title>
    <script type="module" crossorigin src="/assets/index-fc335205.js"></script>
    <link rel="stylesheet" href="/assets/index-3271312e.css">
  </head>
  <body>
    <div id="app"></div>
    
  </body>

  <script id ="bub-shader-vert" class="absolute invisible
  " type="notjs" >#version 300 es
    in vec3 vertPos;
    void main() {
      gl_Position = vec4(vertPos, 1.0);
    }

  </script>

  <script id ="bub-shader-frag" class="absolute invisible
  " type="notjs">#version 300 es

precision highp float;
out vec4 fragColor;
uniform vec3 iResolution;
uniform float iTime;
uniform float iTimeDelta;
uniform float phaseTrans;
uniform float phaseScale;

// noise block

//
// GLSL textureless classic 3D noise "cnoise",
// with an RSL-style periodic variant "pnoise".
// Author:  Stefan Gustavson (stefan.gustavson@liu.se)
// Version: 2011-10-11
//
// Many thanks to Ian McEwan of Ashima Arts for the
// ideas for permutation and gradient selection.
//
// Copyright (c) 2011 Stefan Gustavson. All rights reserved.
// Distributed under the MIT license. See LICENSE file.
// https://github.com/stegu/webgl-noise
//

vec3 mod289(vec3 x) {return x - floor(x * (1.0 / 289.0)) * 289.0;}
vec4 mod289(vec4 x) {return x - floor(x * (1.0 / 289.0)) * 289.0;}
vec4 permute(vec4 x) {return mod289(((x*34.0)+10.0)*x);}
vec4 taylorInvSqrt(vec4 r) {return 1.79284291400159 - 0.85373472095314 * r;}
vec3 fade(vec3 t) {return t*t*t*(t*(t*6.0-15.0)+10.0);}

// Classic Perlin noise
float cnoise(vec3 P)
{
    vec3 Pi0 = floor(P); // Integer part for indexing
    vec3 Pi1 = Pi0 + vec3(1.0); // Integer part + 1
    Pi0 = mod289(Pi0);
    Pi1 = mod289(Pi1);
    vec3 Pf0 = fract(P); // Fractional part for interpolation
    vec3 Pf1 = Pf0 - vec3(1.0); // Fractional part - 1.0
    vec4 ix = vec4(Pi0.x, Pi1.x, Pi0.x, Pi1.x);
    vec4 iy = vec4(Pi0.yy, Pi1.yy);
    vec4 iz0 = Pi0.zzzz;
    vec4 iz1 = Pi1.zzzz;

    vec4 ixy = permute(permute(ix) + iy);
    vec4 ixy0 = permute(ixy + iz0);
    vec4 ixy1 = permute(ixy + iz1);

    vec4 gx0 = ixy0 * (1.0 / 7.0);
    vec4 gy0 = fract(floor(gx0) * (1.0 / 7.0)) - 0.5;
    gx0 = fract(gx0);
    vec4 gz0 = vec4(0.5) - abs(gx0) - abs(gy0);
    vec4 sz0 = step(gz0, vec4(0.0));
    gx0 -= sz0 * (step(0.0, gx0) - 0.5);
    gy0 -= sz0 * (step(0.0, gy0) - 0.5);

    vec4 gx1 = ixy1 * (1.0 / 7.0);
    vec4 gy1 = fract(floor(gx1) * (1.0 / 7.0)) - 0.5;
    gx1 = fract(gx1);
    vec4 gz1 = vec4(0.5) - abs(gx1) - abs(gy1);
    vec4 sz1 = step(gz1, vec4(0.0));
    gx1 -= sz1 * (step(0.0, gx1) - 0.5);
    gy1 -= sz1 * (step(0.0, gy1) - 0.5);

    vec3 g000 = vec3(gx0.x,gy0.x,gz0.x);
    vec3 g100 = vec3(gx0.y,gy0.y,gz0.y);
    vec3 g010 = vec3(gx0.z,gy0.z,gz0.z);
    vec3 g110 = vec3(gx0.w,gy0.w,gz0.w);
    vec3 g001 = vec3(gx1.x,gy1.x,gz1.x);
    vec3 g101 = vec3(gx1.y,gy1.y,gz1.y);
    vec3 g011 = vec3(gx1.z,gy1.z,gz1.z);
    vec3 g111 = vec3(gx1.w,gy1.w,gz1.w);

    vec4 norm0 = taylorInvSqrt(vec4(dot(g000, g000), dot(g010, g010), dot(g100, g100), dot(g110, g110)));
    g000 *= norm0.x;
    g010 *= norm0.y;
    g100 *= norm0.z;
    g110 *= norm0.w;
    vec4 norm1 = taylorInvSqrt(vec4(dot(g001, g001), dot(g011, g011), dot(g101, g101), dot(g111, g111)));
    g001 *= norm1.x;
    g011 *= norm1.y;
    g101 *= norm1.z;
    g111 *= norm1.w;

    float n000 = dot(g000, Pf0);
    float n100 = dot(g100, vec3(Pf1.x, Pf0.yz));
    float n010 = dot(g010, vec3(Pf0.x, Pf1.y, Pf0.z));
    float n110 = dot(g110, vec3(Pf1.xy, Pf0.z));
    float n001 = dot(g001, vec3(Pf0.xy, Pf1.z));
    float n101 = dot(g101, vec3(Pf1.x, Pf0.y, Pf1.z));
    float n011 = dot(g011, vec3(Pf0.x, Pf1.yz));
    float n111 = dot(g111, Pf1);

    vec3 fade_xyz = fade(Pf0);
    vec4 n_z = mix(vec4(n000, n100, n010, n110), vec4(n001, n101, n011, n111), fade_xyz.z);
    vec2 n_yz = mix(n_z.xy, n_z.zw, fade_xyz.y);
    float n_xyz = mix(n_yz.x, n_yz.y, fade_xyz.x);
    return 2.2 * n_xyz;
}
vec3 cPsuedoColor(vec3 p){
    vec3 pg = p.yzx;
    pg.x = -pg.x;
    vec3 pb = p.zxy;
    pb.x = -pg.x;
    return vec3(cnoise(p), cnoise(pg), cnoise(pb));
}
//noise end

vec3 erot(vec3 p, vec3 ax, float ro) {
    return mix(dot(p,ax)*ax,p,cos(ro))+sin(ro)*cross(ax,p);
}

float opSmoothUnion( float d1, float d2, float k ) {
    float h = clamp( 0.5 + 0.5*(d2-d1)/k, 0.0, 1.0 );
    return mix( d2, d1, h ) - k*h*(1.0-h);
}

vec3 translate(vec3 p, vec3 ofs){
    return p - ofs;
}

float clamp01(float x){
    return clamp(x,0.,1.);
}
float scaleFloat (float number,
                     float inMin,
                     float inMax,
                     float outMin,
                     float outMax) {
    return (number - inMin) * (outMax - outMin) / (inMax - inMin) + outMin;
}
float sdSphere(vec3 p, float r){
    return length(p)-r;
}

#define BUB_EX_N 2
vec3[BUB_EX_N] bub_p0 = vec3[](
    vec3(0.4,-0.35,0.0),
    vec3(0.45,-0.25,0.)
);
vec3[BUB_EX_N] bub_p1 = vec3[](
    vec3(0.0,0.7,0),
    vec3(0.1, 0.18, 0.)
);

float [BUB_EX_N] bub_exp = float[](
        1.0,
        0.5
);

// R0, R1, K
vec3[BUB_EX_N] bub_r = vec3[](
        vec3(0, 0.8, 0.75),
        vec3(0, 0.25, .25)
);
// AMP, SCALE, SPD
vec3[BUB_EX_N] bub_noise = vec3[](
    vec3(0.02, 1.6, 1.),
    vec3(0.01, 0.5, 1.)
);

#define NOISE_FLOW 2.5
float map(vec3 p){
    float result = sdSphere(
                                translate(p, vec3(0.47,-0.29,0))
                            ,0.125);

    for (int i = 0; i < BUB_EX_N; i++){
        // mixing
        float fac = clamp01(pow ( phaseTrans, exp2(bub_exp[i])));
        vec3 pos = mix (bub_p0[i], bub_p1[i]  , fac);
        float r = mix (bub_r[i].x, bub_r[i].y  , fac);
        float ks = fac * bub_r[i].z;
        //noise?
        vec3 noise = vec3(0.);

        vec3 posNz = pos + normalize(p-pos) * r;
        float nAmp = bub_noise[i].x;
        float nScale = bub_noise[i].y;
        float nSpd = bub_noise[i].z;

        vec3 cnIn = posNz * nScale;
        cnIn.z -=  iTime * nSpd * NOISE_FLOW / nScale;
        float r1 = r + (cnoise( cnIn) *2.0 - 1.0 ) * nAmp;

        result = opSmoothUnion(result,
                               sdSphere(translate(
                                       p, pos + noise
                                             ), r1),
                               ks
        );
    }

    return result;
}
#define HIT_EPSL 0.0001
#define NORM_EPSL 0.005
vec3 norm(vec3 p){
    vec2 e = vec2(NORM_EPSL,0.);
    return normalize( map(p)-vec3(
        map(p-e.xyy),
        map(p-e.yxy),
        map(p-e.yyx)));
}
void main() {
    vec2 uv = vec2(2.*gl_FragCoord.x,2.*gl_FragCoord.y)/iResolution.x;
    uv+=vec2(-1.0,-iResolution.y/iResolution.x);
    uv /= phaseScale;
    float d =1.0e10;
    bool hit = false;
    vec3 trace =vec3(uv.x,uv.y, -10.0);
    vec3 dir = vec3(0,0,1);
    for (int i =0; i<24; i++){
        d= map(trace);
        if (d < HIT_EPSL) {hit=true;break;}
        trace+=d*dir;
        if (trace.z > 10.) break;
    }

    float u =uv.x, v=uv.y;
    fragColor = vec4(0.);
    if (hit){


        vec3 nm = norm(trace);
        float v = dot(nm, vec3(0.,0.,-1.));
        v = clamp01(1.-v);
        float vOrig = v;
        v = scaleFloat( v, 0.3,.5, 0.,1. );
        //v = clamp01(v);
        float av = pow (v, 1.);


        //vec3 colInner = vec3(abs(uv),0.5);
        vec3 colInner = cPsuedoColor(nm + vec3(0.,0.,iTime*3.));
        colInner += 1.0;
        colInner /= 2.0;
        fragColor = vec4(colInner,av);

        //riming
        float fRim = 0.;
        float vRim = vOrig;
        if (vRim>0.59) fRim = 1.;

        vec3 lDir = vec3(0.,  -1.,  0.);
        lDir = erot(lDir, vec3(1.,0.,0.), radians(-35.));
        lDir = erot(lDir, vec3(0.,1.,0.), radians(-40.));

        // first rim

        vec3 colRim = vec3(0.8,0.8,1.);

        fragColor = mix (fragColor, vec4(colRim, 1.), fRim );
        // second Rim
        vec3 dirSum = normalize(lDir + dir);
        fRim = 1.;//
        float vBackRim = dot (dirSum, nm);
        float br0 = -.1; float br1 = br0 + 0.005;
        vBackRim = scaleFloat( vBackRim, br0,br1, 0.,1.);
        vBackRim = clamp01(vBackRim);
        fRim *= vBackRim;

        vec3 colBackRim = vec3(0.5,.98,1.);

        fragColor = mix (fragColor, vec4(colBackRim, 1.), vBackRim );

        // spec

        float vSpec = dot (dirSum, -nm);
        br0 = .96; br1 = br0 + 0.005;
        vSpec = scaleFloat( vSpec, br0,br1, 0.,1.);
        vSpec = clamp01(vSpec);
        vec3 colSpec = vec3(1.);
        fragColor = mix (fragColor, vec4(colSpec, 1.), vSpec );


    }
}</script>
  <script id ="bub-shader-fraguppa" class="absolute invisible
  " type="glsl" src="./src/assets/test.glsl"></script>
  <script id ="bub-shader-frag-ext" class="absolute invisible
  " type="glsl" src="./src/assets/bub.frag"></script>
</html>
